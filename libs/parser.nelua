-- TODO: remove 'for _, param in ipairs(node.params) do' from cgen and put in parse_proc_def

require "map-nelua.map"
require "switch-nelua.switch"

local varaint = require "variant-nelua.variant"
local Result = varaint.Result
local Option = varaint.Option

local Pos = require "..generic.pos"
local lang_info = require "..generic.lang_info"

local pos_to_line_col = require ".pos_to_line_col"

local tokenizer = require ".tokenizer"
local Token = tokenizer.Token
local TokenKind = tokenizer.TokenKind
local TokenKind_tostring = tokenizer.TokenKind_tostring

local ErrLoc = require ".errloc"
local ErrLocKind = ErrLoc.ErrLocKind

local Parser = @record{
  path: string,
  index: integer,
  content: string,
  tokens: sequence(Token)
}

local function new_error(kind: ErrLocKind, parser: *Parser, err: string, s_token: Token, e_token: Token): ErrLoc
  range_check!(kind, ErrLocKind)
  assert(parser)
  assert(err ~= "")
  range_check!(s_token.kind, TokenKind)
  range_check!(e_token.kind, TokenKind)

  return {
    kind = kind,

    path = parser.path,
    content = parser.content,
    err = err,

    s_index = s_token.s_index,
    e_index = e_token.e_index,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    e_pos = {
      line = e_token.e_pos.line,
      col = e_token.e_pos.col,
    },
  }
end

local function error_unexpected_token(parser: *Parser, token: Token): ErrLoc
  assert(parser)
  range_check!(token.kind, TokenKind)

  return new_error(
    ErrLocKind.SYNTAX,
    parser,
    ("Unexpected Token '%s'"):format(token.val),
    token,
    token
  )
end

local function error_redeclaration(parser: *Parser, token: *Token): ErrLoc
  assert(parser)
  range_check!(token.kind, TokenKind)

  return new_error(
    ErrLocKind.SEMANTIC,
    parser,
    ("Redeclaration of identifier '%s'"):format(token.val),
    token,
    token
  )
end

local function error_undefined_identifier(parser: *Parser, token: *Token): ErrLoc
  assert(parser)
  range_check!(token.kind, TokenKind)

  return new_error(
    ErrLocKind.SEMANTIC,
    parser,
    ("Undefinded identifier '%s'"):format(token.val),
    token,
    token
  )
end

local NewParserResult = @Result(Parser, ErrLoc)
function Parser.new(path: string, content: string): NewParserResult
  local tokenize_res = tokenizer.tokenize(path, content)
  if tokenize_res:is_err() then
    return NewParserResult.err(tokenize_res:get_err())
  end
  return NewParserResult.ok({
    path = path,
    index = 1,
    content = content,
    tokens = tokenize_res:get_ok(),
  })
end

function Parser:peek()
  if self.index <= #self.tokens then
    return self.tokens[self.index]
  end

  local line, col = pos_to_line_col(#self.content, self.content)
  return Token{
    kind = TokenKind.EOF,
    val = "",
    s_pos = {
      line = line,
      col = col,
    },
    e_pos = {
      line = line,
      col = col,
    },
    s_index = #self.content,
    e_index = #self.content,
  }
end

function Parser:peek_next()
  if self.index + 1 <= #self.tokens then
    return self.tokens[self.index + 1]
  end

  local line, col = pos_to_line_col(#self.content, self.content)
  return Token{
    kind = TokenKind.EOF,
    val = "",
    s_pos = {
      line = line,
      col = col,
    },
    e_pos = {
      line = line,
      col = col,
    },
    s_index = #self.content,
    e_index = #self.content,
  }
end

function Parser:peek_prev()
  if self.index - 1 <= #self.tokens then
    return self.tokens[self.index - 1]
  end

  local line, col = pos_to_line_col(#self.content, self.content)
  return Token{
    kind = TokenKind.EOF,
    val = "",
    s_pos = {
      line = line,
      col = col,
    },
    e_pos = {
      line = line,
      col = col,
    },
    s_index = #self.content,
    e_index = #self.content,
  }
end

function Parser:advance()
  local token  = self:peek()
  self.index = self.index + 1
  return token
end

local ExpectResult = @Result(Token, ErrLoc)

function Parser:expect_one_of(token_kinds: sequence(TokenKind), msg: string): ExpectResult
  assert(#token_kinds > 0)

  local token = self:advance()

  for _, token_kind in ipairs(token_kinds) do
    range_check!(token_kind, TokenKind)
    if token_kind == token.kind then
      return ExpectResult.ok(token)
    end
  end

  local sb: stringbuilder
  if msg ~= "" then
    sb:write(msg)
  elseif #token_kinds == 1 then
    sb:write("Expected TokenKind '", TokenKind_tostring(token_kinds[1]), "' but got '", TokenKind_tostring(token.kind), "'")
  else
    sb:write("Expected one of TokenKinds '{ ")
    for _, token_kind in  ipairs(token_kinds) do
      range_check!(token_kind, TokenKind)
      sb:write(TokenKind_tostring(token_kind))
      sb:write(", ")
    end
    sb:rollback(2)
    sb:write(" }' but got '", TokenKind_tostring(token.kind), "'")
  end

  return ExpectResult.err{
    path = self.path,
    content = self.content,
    s_index = token.s_index,
    e_index = token.e_index,
    err = sb:promote(),
    kind = ErrLocKind.SYNTAX,
    s_pos = {
      line = token.s_pos.line,
      col = token.s_pos.col,
    },
    e_pos = {
      line = token.e_pos.line,
      col = token.e_pos.col,
    },
  }
end

function Parser:expect(token_kind: TokenKind, msg: string): ExpectResult
  return self:expect_one_of({ token_kind }, msg)
end

function Parser:is(token_kinds: sequence(TokenKind)): boolean

  for _, kind in ipairs(token_kinds) do
    if kind == self:peek().kind then
      return true
    end
  end

  return false
end

function Parser:advance_comma_unless(token_kind: TokenKind): ExpectResult
  range_check!(token_kind, TokenKind)

  local token = self:peek()
  if token.kind == TokenKind.COMMA then
    if self:peek_next().kind == token_kind or self:peek_next().kind == TokenKind.EOF then
      return ExpectResult.err({
        path = self.path,
        content = self.content,
        s_index = token.s_index,
        e_index = token.e_index,
        err = "Trailing comma",
        kind = ErrLocKind.SYNTAX,
        s_pos = {
          line = token.s_pos.line,
          col = token.s_pos.col,
        },
        e_pos = {
          line = token.e_pos.line,
          col = token.e_pos.col,
        },
      })
    end
    return ExpectResult.ok(self:advance())
  end

  if token.kind ~= token_kind and token.kind ~= TokenKind.EOF then
    return ExpectResult.err({
      path = self.path,
      content = self.content,
      s_index = token.s_index,
      e_index = token.e_index,
      err = ("Expected Token value ',' but got '%s'"):format(token.val),
      kind = ErrLocKind.SYNTAX,
      s_pos = {
        line = token.s_pos.line,
        col = token.s_pos.col,
      },
      e_pos = {
        line = token.e_pos.line,
        col = token.e_pos.col,
      },
    })
  end

  return ExpectResult.ok(token)
end

function Parser:advance_comma_until(token_kind: TokenKind): ExpectResult
  range_check!(token_kind, TokenKind)

  local token = self:peek()
  if token.kind == TokenKind.COMMA then
    return ExpectResult.ok(self:advance())
  end

  if token.kind ~= token_kind and token.kind ~= TokenKind.EOF then
    return ExpectResult.err({
      path = self.path,
      content = self.content,
      s_index = token.s_index,
      e_index = token.e_index,
      err = ("Expected Token value ',' but got '%s'"):format(token.val),
      kind = ErrLocKind.SYNTAX,
      s_pos = {
        line = token.s_pos.line,
        col = token.s_pos.col,
      },
      e_pos = {
        line = token.e_pos.line,
        col = token.e_pos.col,
      },
    })
  end

  return ExpectResult.ok(token)
end

local Node <forwarddecl> = @record{}

local NodeKind = @enum{
  _UNDEFINED = 0,
  ARRAY,
  ASSIGNMENT,
  AST,
  BIN_EXPR,
  BREAK,
  COMMENT,
  ELIF_STMT,
  ELSE_STMT,
  FIELD,
  FOR_LOOP,
  ID,
  ID_DECL,
  IF_STMT,
  INDEX,
  INIT_FIELD,
  INIT_LIST,
  LITERAL,
  PROC_CALL,
  PROC_DEF,
  RETURN,
  TYPE,
  WHILE_LOOP,
  _COUNT
}

local function NodeKind_tostring(kind: NodeKind): string
  range_check!(kind, NodeKind)
  ## for _, field in ipairs(NodeKind.value.fields) do
    if #[field.value]# == kind then
      return #[field.name]#
    end
  ## end
  error("UNREACHABLE")
  return ""
end

local Annotations = @record{
  constant: boolean
}

Node = @record{
  kind: NodeKind,

  s_pos: Pos,
  e_pos: Pos,
  s_index: integer,
  e_index: integer,

  name: string,
  type: *Node,

  children: sequence(*Node),
  scope: *Node,

  annotations: Annotations,

  -- Scope
  content: string,

  -- ProcDef
  params: sequence(*Node),
  ret_t: *Node,

  -- ProcCall
  args: sequence(*Node),

  id: *Node,

  -- BinOp
  lhs: *Node,
  op: Token,
  rhs: *Node,

  -- WhileLoop|ForLoop|IfStmt
  cond: *Node,
  step: *Node,

  -- Literal
  lit_val: string,

  -- IdDecl
  used: boolean,

  -- Array
  len: uinteger,

  -- Record
  fields: sequence(*Node),
  is_builtin: boolean,
  is_boolean: boolean,
  is_scalar: boolean,
  is_integral: boolean,
  is_float: boolean,
  is_string: boolean,
  is_array: boolean,
  is_pointer: boolean,
  is_proc: boolean,
  is_niltype: boolean,
  is_any: boolean,
  is_type_type: boolean,

  is_type_decl: boolean,

  is_rec: boolean,
  is_type: boolean,

  is_i64: boolean,
  is_u64: boolean,
  is_u8: boolean,
  is_f64: boolean,

  is_anonymous: boolean,
  is_unbounded: boolean,

  val: *Node,
}

function Node:tostring(indent: integer, inline: boolean): string
  range_check!(self.kind, NodeKind)

  local indent_str = string.rep("  ", indent)
  local sb: stringbuilder
  if not self.is_type_decl then
    if not inline then
      sb:write(indent_str)
    end
    sb:write(NodeKind_tostring(self.kind))
  end
  switch self.kind do
    case NodeKind.AST then
      sb:write("\n")
    case NodeKind.ID_DECL then
      if not self.is_type_decl then
        sb:write(" -> ", self.name, ": ", self.type:tostring(indent, true))
        if self.val then
          sb:write(" = ", self.val:tostring(indent, true))
        end
        if not inline then
          sb:write("\n")
        end
      end
    case NodeKind.LITERAL then
      sb:write("(")
      if self.type.is_string then
        sb:write('"')
      end
      sb:write(self.lit_val)
      if self.type.is_string then
        sb:write('"')
      end
      sb:write(")")
    case NodeKind.TYPE then
      sb:write(" ")
      if self.is_rec then
        sb:write(self.name, "{\n")
        for i, field in ipairs(self.fields) do
          sb:write(field:tostring(indent + 1))
        end
        sb:write(indent_str, "}")
      else
        sb:write(self.name)
      end
      if not inline then
        sb:write("\n")
      end
    case NodeKind.ID then
      sb:write(" -> ", self.name, ": ", self.type:tostring(indent + 1, true))
    case NodeKind.PROC_CALL then
      sb:write(" ", self.name, "(\n")
      for _, arg in ipairs(self.args) do
        sb:write(arg:tostring(indent + 1))
      end
      sb:write("\n", indent_str ,")\n")
    case NodeKind.FIELD then
      sb:write(" -> ", self.id.name ,".", self.name, ": ", self.type:tostring(indent + 1, true))
    case NodeKind.WHILE_LOOP, NodeKind.IF_STMT, NodeKind.ELIF_STMT then
      sb:write(" -> ", self.cond:tostring(indent + 1, true), "\n")
    case NodeKind.ELSE_STMT then
      sb:write("\n")
    case NodeKind.BREAK then
      sb:write("\n")
    case NodeKind.BIN_EXPR then
      sb:write(
        "(",
        self.lhs:tostring(indent + 1, true),
        " OP(",
        self.op.val,
        ") ",
        self.rhs:tostring(indent + 1, true),
        ")"
      )
    case NodeKind.ASSIGNMENT then
      sb:write(
        " -> ",
        self.id:tostring(indent + 1, true),
        ": ", self.id.type:tostring(indent + 1, true),
        " = ", self.val:tostring(indent + 1, true),
        "\n"
      )
    case NodeKind.COMMENT then
      sb:write("\n")
    case NodeKind.FOR_LOOP then
      sb:write(" -> (", self.id:tostring(indent + 1, true), " ; " , self.cond:tostring(indent + 1, true))
      if self.step then
        sb:write(" ; ", self.step:tostring(indent + 1, true))
      end
      sb:write(")\n")
    case NodeKind.ARRAY then
      fallthrough
    case NodeKind.INDEX then
      fallthrough
    case NodeKind.INIT_FIELD then
      fallthrough
    case NodeKind.INIT_LIST then
      fallthrough
    case NodeKind.PROC_DEF then
      fallthrough
    case NodeKind.RETURN then
      fallthrough
    else
      error(("TODO: Node:tostring '%s'"):format(NodeKind_tostring(self.kind)))
  end
  if self.children then
    for _, child in ipairs(self.children) do
      sb:write(child:tostring(indent + 1))
    end
  end
  return sb:promote()
end

function Node:is_scope(): boolean
  switch self.kind do
    case
      NodeKind.AST,
      NodeKind.PROC_DEF,
      NodeKind.WHILE_LOOP,
      NodeKind.FOR_LOOP,
      NodeKind.IF_STMT,
      NodeKind.ELIF_STMT,
      NodeKind.ELSE_STMT
    then
      return true
  end
  return false
end

function Node:is_loop(): boolean
  switch self.kind do
    case
      NodeKind.WHILE_LOOP
    then
      return true
  end
  return false
end

local type_type: Node = {
  kind = NodeKind.TYPE,
  name = "type",
  is_builtin = true,
  is_type_type = true,
  is_type = true
}
type_type.type = &type_type

local i64_type: Node = {
  kind = NodeKind.TYPE,
  name = "i64",
  is_builtin = true,
  is_scalar = true,
  is_integral = true,
  is_i64 = true,
  is_type = true
}
i64_type.type = &i64_type

local f64_type: Node = {
  kind = NodeKind.TYPE,
  name = "f64",
  is_builtin = true,
  is_scalar = true,
  is_float = true,
  is_f64 = true,
  is_type = true
}
f64_type.type = &f64_type

local u64_type: Node = {
  kind = NodeKind.TYPE,
  name = "u64",
  is_builtin = true,
  is_scalar = true,
  is_integral = true,
  is_u64 = true,
  is_type = true
}
u64_type.type = &u64_type

local u8_type: Node = {
  kind = NodeKind.TYPE,
  name = "u8",
  is_builtin = true,
  is_scalar = true,
  is_integral = true,
  is_u8 = true,
  is_type = true
}
u8_type.type = &u8_type

local string_data_type: Node = {
  kind = NodeKind.TYPE,
  name = "*[0]u8",
  val = &u8_type,
  len = 0,
  is_array = true,
  is_unbounded = true,
  is_type = true
}
string_data_type.type = &string_data_type

local string_data: Node = {
  kind = NodeKind.ID_DECL,
  name = "data",
  type = &string_data_type,
}

local string_size: Node = {
  kind = NodeKind.ID_DECL,
  name = "size",
  type = &u64_type,
}

local string_type: Node = {
  kind = NodeKind.TYPE,
  name = "string",
  is_builtin = true,
  is_string = true,
  is_rec = true,
  is_anonymous = true,
  fields = {
    &string_data,
    &string_size,
  },
  is_type = true
}
string_type.type = &string_type

local niltype_type: Node = {
  kind = NodeKind.TYPE,
  name = "niltype",
  is_builtin = true,
  is_niltype = true,
  is_type = true
}
niltype_type.type = &niltype_type

local boolean_type: Node = {
  kind = NodeKind.TYPE,
  name = "boolean",
  is_builtin = true,
  is_boolean = true,
  is_type = true
}
boolean_type.type = &boolean_type

local any_type: Node = {
  kind = NodeKind.TYPE,
  name = "any",
  is_builtin = true,
  is_any = true,
  is_type = true
}
any_type.type = &any_type

local Np = @*Node
local builtin_types = map!(string, Np, {
  [type_type.name] = &type_type,
  [i64_type.name] = &i64_type,
  [f64_type.name] = &f64_type,
  [u64_type.name] = &u64_type,
  [u8_type.name] = &u8_type,
  [string_type.name] = &string_type,
  [boolean_type.name] = &boolean_type,
  [niltype_type.name] = &niltype_type,
  [any_type.name] = &any_type,
})

local BinOpInfo = @record{
  op: TokenKind,
  type: *Node,
  possible_lht: sequence(*Node),
  possible_rht: sequence(*Node),
}

local bin_ops = map!(TokenKind, BinOpInfo, {
  [TokenKind.EQUATES] = {
    op = TokenKind.OPEN_ANGLE,
    type = builtin_types[boolean_type.name],
    possible_lht = { builtin_types[any_type.name] },
    possible_rht = { builtin_types[any_type.name] },
  },
  [TokenKind.OPEN_ANGLE] = {
    op = TokenKind.OPEN_ANGLE,
    type = builtin_types[boolean_type.name],
    possible_lht = { builtin_types[i64_type.name] },
    possible_rht = { builtin_types[i64_type.name] },
  },
  [TokenKind.CLOSE_ANGLE] = {
    op = TokenKind.CLOSE_ANGLE,
    type = builtin_types[boolean_type.name],
    possible_lht = { builtin_types[i64_type.name] },
    possible_rht = { builtin_types[i64_type.name] },
  },
  [TokenKind.PLUS] = {
    op = TokenKind.PLUS,
    type = builtin_types[i64_type.name],
    possible_lht = { builtin_types[i64_type.name] },
    possible_rht = { builtin_types[i64_type.name] },
  },
  [TokenKind.MINUS] = {
    op = TokenKind.MINUS,
    type = builtin_types[i64_type.name],
    possible_lht = { builtin_types[i64_type.name] },
    possible_rht = { builtin_types[i64_type.name] },
  },
  [TokenKind.STAR] = {
    op = TokenKind.STAR,
    type = builtin_types[i64_type.name],
    possible_lht = { builtin_types[i64_type.name] },
    possible_rht = { builtin_types[i64_type.name] },
  },
})

local BuiltinProc = @enum{
  _UNDEFINED = 0,
  PRINT,
  _COUNT
}

local builtin_procs = map!(string, BuiltinProc, {
  ["print"] = BuiltinProc.PRINT,
})

function Parser:new_literal(scope: *Node, n_type: *Node): *Node
  assert(scope and scope:is_scope())

  local token = self:advance()

  local node = new(Node)
  $node = {
    kind = NodeKind.LITERAL,

    s_pos = {
      line = token.s_pos.line,
      col = token.s_pos.col,
    },
    e_pos = {
      line = token.e_pos.line,
      col = token.e_pos.col,
    },
    s_index = token.s_index,
    e_index = token.e_index,
    scope = scope,

    type = n_type,
    lit_val = token.val,
  }

  return node
end

function Parser:check_node_type(typ: *Node, node: *Node): Option(ErrLoc)
  assert(typ and (typ.kind == NodeKind.TYPE or (typ.kind == NodeKind.ID and typ.type.kind == NodeKind.TYPE)))
  assert(node)

  -- if node.kind == NodeKind.INIT_LIST then
  --   if typ.kind == NodeKind.ID and typ.type.is_rec then
  --     return self:check_rec_fields(typ, node)
  --   else
  --     return Option.Some(ErrLoc{
  --       kind = ErrLocKind.SEMANTIC,

  --       path = self.path,
  --       content = self.content,
  --       err = ("type '%s' cannot be initialized using an initializer list"):format(typ.name),

  --       s_index = node.s_index,
  --       e_index = node.e_index,
  --       s_pos = {
  --         line = node.s_pos.line,
  --         col = node.s_pos.col,
  --       },
  --       e_pos = {
  --         line = node.e_pos.line,
  --         col = node.e_pos.col,
  --       },
  --     })
  --   end
  -- end

  if typ.is_array and node.type.is_array then
    if typ.len >= node.type.len and typ.val == node.type.val then
      return Option.None()
    end
  elseif typ.is_type_type and node.type.is_rec then
    $typ = $node.type
    return Option.None()
  elseif typ.is_any then
    return Option.None()
  else
    if typ == node.type then
      return Option.None()
    end
  end

  return Option.Some(ErrLoc{
    kind = ErrLocKind.SEMANTIC,

    path = self.path,
    content = self.content,
    err = ("Type '%s' does not match type '%s'"):format(node.type.name, typ.name),

    s_index = node.s_index,
    e_index = node.e_index,
    s_pos = {
      line = node.s_pos.line,
      col = node.s_pos.col,
    },
    e_pos = {
      line = node.e_pos.line,
      col = node.e_pos.col,
    },
  })
end

function Parser:new_binary_expr(scope: *Node, lhs: *Node, op: Token, rhs: *Node): *Node
  assert(scope and scope:is_scope())
  assert(lhs)
  range_check!(op.kind, TokenKind)
  assert(rhs)

  assert(bin_ops:has(op.kind), op.val)
  local bin_op_info = bin_ops[op.kind]

  local ok: boolean
  local err_loc: ErrLoc
  for _, lht in ipairs(bin_op_info.possible_lht) do
    local err_loc_opt = self:check_node_type(lht, lhs)
    if err_loc_opt:is_none() then
      ok = true
    else
      err_loc = err_loc_opt:get_some()
    end
  end
  if not ok then
    error(err_loc:tostring())
  end

  ok = false
  for _, rht in ipairs(bin_op_info.possible_rht) do
    local err_loc_opt = self:check_node_type(rht, rhs)
    if err_loc_opt:is_none() then
      ok = true
    else
      err_loc = err_loc_opt:get_some()
    end
  end
  if not ok then
    error(err_loc:tostring())
  end

  local binary_expr = new(Node)
  $binary_expr = {
    kind = NodeKind.BIN_EXPR,

    s_pos = {
      line = lhs.s_pos.line,
      col = lhs.s_pos.col
    },
    e_pos = {
      line = rhs.e_pos.line,
      col = rhs.e_pos.col
    },
    s_index = lhs.s_index,
    e_index = rhs.e_index,
    scope = scope,

    lhs = lhs,
    op = op,
    rhs = rhs,
    type = bin_op_info.type
  }
  return binary_expr
end

function Parser:new_proc_call(s_token: Token, e_token: Token, scope: *Node, args: sequence(*Node), proc_type: *Node): *Node
  range_check!(s_token.kind, TokenKind)
  range_check!(e_token.kind, TokenKind)
  assert(scope and scope:is_scope())
  assert(proc_type)

  local proc_call = new(Node)
  $proc_call = {
    kind = NodeKind.PROC_CALL,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    e_pos = {
      line = e_token.s_pos.line,
      col = e_token.s_pos.col,
    },
    s_index = s_token.s_index,
    e_index = e_token.e_index,
    scope = scope,

    name = s_token.val,
    args = args,
    type = proc_type,
  }

  return proc_call
end

local ParseResult = @Result(*Node, ErrLoc)

function Parser.find_id_decl_up_scope(scope: *Node, name: string, s_index: integer): Option(*Node)
  assert(scope and scope:is_scope())

  for _, node in ipairs(scope.children) do
    if (node.kind == NodeKind.ID_DECL or node.kind == NodeKind.PROC_DEF) and node.name == name and node.s_index <= s_index then
      node.used = true
      return Option.Some(node)
    end
  end

  if scope.scope then
    return Parser.find_id_decl_up_scope(scope.scope, name, s_index)
  end
  return Option.None()
end

local Parser.parse_expr: function(self: *Parser, scope: *Node): ParseResult
local Parser.parse_scope: function(self: *Parser, scope: *Node, till: TokenKind): ParseResult

function Parser:parse_id(scope: *Node): ParseResult
  local expect_res = self:expect(TokenKind.IDENTIFIER)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local token = expect_res:get_ok()

  local id_decl_opt: Option(*Node) = Parser.find_id_decl_up_scope(scope, token.val, token.s_index)
  if id_decl_opt:is_none() then
    return ParseResult.err({
      path = self.path,
      content = self.content,
      s_index = token.s_index,
      e_index = token.e_index,
      err = ("Undeclared identifier '%s'"):format(token.val),
      kind = ErrLocKind.SEMANTIC,
      s_pos = {
        line = token.s_pos.line,
        col = token.s_pos.col,
      },
      e_pos = {
        line = token.e_pos.line,
        col = token.e_pos.col,
      },
    })
  end

  local id_decl_node = id_decl_opt:get_some()

  local node: *Node = new(Node)
  $node = {
    kind = NodeKind.ID,
    s_pos = {
      line = token.s_pos.line,
      col = token.s_pos.col,
    },
    e_pos = {
      line = token.e_pos.line,
      col = token.e_pos.col,
    },
    s_index = token.s_index,
    e_index = token.e_index,

    name = token.val,
    type = id_decl_node.type,
    scope = scope,
  }
  return ParseResult.ok(node)
end

function Parser:parse_print(scope: *Node, token: Token): ParseResult
  assert(scope and scope:is_scope())
  range_check!(token.kind, TokenKind)

  local s_token = token

  assert(s_token.val == "print")

  local expect_res = self:expect(TokenKind.OPEN_PAREN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local print_args: sequence(*Node)
  while not self:is({ TokenKind.CLOSE_PAREN }) do
    local parse_res: ParseResult = self:parse_expr(scope)
    if parse_res:is_err() then
      return parse_res
    end

    print_args:push(parse_res:get_ok())

    expect_res = self:advance_comma_unless(TokenKind.CLOSE_PAREN)
    if expect_res:is_err() then
      return ParseResult.err(expect_res:get_err())
    end
  end

  local expect_res = self:expect(TokenKind.CLOSE_PAREN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  assert(builtin_types:has("niltype"))
  return ParseResult.ok(self:new_proc_call(s_token, e_token, scope, print_args, builtin_types["niltype"]))
end

function Parser:parse_proc_call(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.IDENTIFIER)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()
  local token = s_token
  local proc_call_name = token.val

  if builtin_procs:has(proc_call_name) then
    if builtin_procs[proc_call_name] == BuiltinProc.PRINT then
      return self:parse_print(scope, s_token)
    else
      error(("Unhandled builtin proc '%s'"):format(proc_call_name))
    end
  end

  local proc_id_decl_opt = Parser.find_id_decl_up_scope(scope, token.val, token.s_index)
  if proc_id_decl_opt:is_none() then
    return ParseResult.err(error_undefined_identifier(self, token))
  end

  local proc_id_decl = proc_id_decl_opt:get_some()
  if not proc_id_decl.type.is_proc then
    return ParseResult.err({
      path = self.path,
      content = self.content,
      s_index = token.s_index,
      e_index = token.e_index,
      err = ("Identifier '%s' is not a function"):format(proc_id_decl.name),
      kind = ErrLocKind.SYNTAX,
      s_pos = {
        line = token.s_pos.line,
        col = token.s_pos.col,
      },
      e_pos = {
        line = token.e_pos.line,
        col = token.e_pos.col,
      },
    })
  end

  expect_res = self:expect(TokenKind.OPEN_PAREN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local proc_type = proc_id_decl.type

  local index = 1
  local proc_call_args: sequence(*Node)
  while not self:is({ TokenKind.CLOSE_PAREN }) do
    local parse_res = self:parse_expr(scope)
    if parse_res:is_err() then
      return parse_res
    end
    local arg = parse_res:get_ok()

    if index > #proc_type.params then
      error("Unexpected argument")
    end

    local err_loc_opt = self:check_node_type(proc_type.params[index].type, arg)
    if err_loc_opt:is_some() then
      return ParseResult.err(err_loc_opt:get_some())
    end

    proc_call_args:push(arg)

    expect_res = self:advance_comma_unless(TokenKind.CLOSE_PAREN)
    if expect_res:is_err() then
      return ParseResult.err(expect_res:get_err())
    end

    index = index + 1
  end

  expect_res = self:expect(TokenKind.CLOSE_PAREN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  if index - 1 ~= #proc_type.params then
    return ParseResult.err(new_error(
      ErrLocKind.SEMANTIC,
      self,
      ("proc '%s' is expecting '%d' argument(s) but got '%d'"):format(proc_call_name, #proc_type.params, index - 1),
      s_token,
      e_token
    ))
  end

  return ParseResult.ok(self:new_proc_call(s_token, e_token, scope, proc_call_args, proc_type.ret_t))
end

function Parser:parse_primary(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local token = self:peek()
  if self:is({ TokenKind.CONSTANT_FALSE, TokenKind.CONSTANT_TRUE }) then
    return ParseResult.ok(self:new_literal(scope, builtin_types["boolean"]))
  elseif self:is({ TokenKind.INTEGER }) then
    return ParseResult.ok(self:new_literal(scope, builtin_types["i64"]))
  elseif self:is({ TokenKind.FLOAT }) then
    return ParseResult.ok(self:new_literal(scope, builtin_types["f64"]))
  elseif self:is({ TokenKind.STRING }) then
    --TODO: Not checking if this is correct since I want to keep track of string size
    return ParseResult.ok(self:new_literal(scope, builtin_types["string"]))
  elseif self:is({ TokenKind.CONSTANT_NIL }) then
    return ParseResult.ok(self:new_literal(scope, builtin_types["niltype"]))
  elseif self:is({ TokenKind.IDENTIFIER }) then
    if self:peek_next().kind == TokenKind.OPEN_PAREN then
      return self:parse_proc_call(scope)
    end
    return self:parse_id(scope)
  elseif self:is({ TokenKind.OPEN_PAREN }) then
    local parse_res = self:parse_expr(scope)
    if parse_res:get_err() then
      return parse_res
    end
    local expr = parse_res:get_ok()
    local expect_res = self:expect(TokenKind.CLOSE_PAREN)
    if expect_res:is_err() then
      return ParseResult.err(expect_res:get_err())
    end
    local e_token = expect_res:get_ok()

    error("PAREN_EXPR")
    local paren_expr = new(Node)
    $paren_expr = {}
    return ParseResult.ok(paren_expr)
  end

  print(token.val)
  error("Unexpected")
  return
end

function Parser:parse_unary(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  if self:is({TokenKind.MINUS, TokenKind.PLUS}) then
    local op_token = self:advance()

    error("UNARY")
    local unary = new(Node)
    $unary = {}
    return ParseResult.ok(unary)
  end

  return self:parse_primary(scope)
end

function Parser:parse_factor(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local parse_res = self:parse_unary(scope)
  if parse_res:is_err() then
    return parse_res
  end

  local lhs = parse_res:get_ok()

  while self:is({ TokenKind.STAR }) do
    local op_token = self:advance()

    local parse_res = self:parse_unary(scope)
    if parse_res:is_err() then
      return parse_res
    end
    local rhs = parse_res:get_ok()

    parse_res = ParseResult.ok(self:new_binary_expr(scope, lhs, op_token, rhs))
    if parse_res:is_err() then
      return parse_res
    end
    lhs = parse_res:get_ok()
  end

  return ParseResult.ok(lhs)
end

function Parser:parse_term(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local parse_res = self:parse_factor(scope)
  if parse_res:is_err() then
    return parse_res
  end

  local lhs = parse_res:get_ok()

  while self:is({ TokenKind.MINUS, TokenKind.PLUS }) do
    local op_token = self:advance()

    local parse_res = self:parse_factor(scope)
    if parse_res:is_err() then
      return parse_res
    end
    local rhs = parse_res:get_ok()

    parse_res = ParseResult.ok(self:new_binary_expr(scope, lhs, op_token, rhs))
    if parse_res:is_err() then
      return parse_res
    end
    lhs = parse_res:get_ok()
  end

  return ParseResult.ok(lhs)
end

function Parser:parse_comparison(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local parse_res = self:parse_term(scope)
  if parse_res:is_err() then
    return parse_res
  end

  local lhs = parse_res:get_ok()

  while self:is({ TokenKind.OPEN_ANGLE, TokenKind.CLOSE_ANGLE, TokenKind.LT_EQS, TokenKind.GT_EQS }) do
    local op_token = self:advance()

    local parse_res = self:parse_term(scope)
    if parse_res:is_err() then
      return parse_res
    end
    local rhs = parse_res:get_ok()

    parse_res = ParseResult.ok(self:new_binary_expr(scope, lhs, op_token, rhs))
    if parse_res:is_err() then
      return parse_res
    end
    lhs = parse_res:get_ok()
  end

  return ParseResult.ok(lhs)
end

function Parser:parse_equality(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local parse_res = self:parse_comparison(scope)
  if parse_res:is_err() then
    return parse_res
  end

  local lhs = parse_res:get_ok()

  while self:is({ TokenKind.NOT_EQS, TokenKind.EQUATES }) do
    local op_token = self:advance()

    local parse_res = self:parse_comparison(scope)
    if parse_res:is_err() then
      return parse_res
    end
    local rhs = parse_res:get_ok()

    parse_res = ParseResult.ok(self:new_binary_expr(scope, lhs, op_token, rhs))
    if parse_res:is_err() then
      return parse_res
    end
    lhs = parse_res:get_ok()
  end

  return ParseResult.ok(lhs)
end

Parser.parse_expr = function(self: *Parser, scope: *Node): ParseResult
  assert(scope and scope:is_scope())
  return self:parse_equality(scope)
end

function Parser:parse_type(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local token = self:peek()
  if not self:is({ TokenKind.IDENTIFIER }) then
    return ParseResult.err(error_unexpected_token(self, token))
  end

  if self:is({ TokenKind.IDENTIFIER }) then
    if builtin_types:has(token.val) then
      self:advance()
      return ParseResult.ok(builtin_types[token.val])
    end
  end

  return ParseResult.err(error_unexpected_token(self, token))
end

function Parser:parse_id_decl(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.IDENTIFIER)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()
  local token = s_token
  local id_name = token.val

  local id_opt = Parser.find_id_decl_up_scope(scope, id_name, token.s_index)
  if id_opt:is_some() then
    return ParseResult.err(error_redeclaration(self, token))
  end

  expect_res = self:expect(TokenKind.COLON)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local id_type: *Node
  if self:is({ TokenKind.IDENTIFIER }) then
    local parse_res = self:parse_type(scope)
    if parse_res:is_err() then
      return parse_res
    end

    id_type = parse_res:get_ok()
  end

  local id_val: *Node
  if self:is({ TokenKind.EQUALS }) then
    expect_res = self:expect(TokenKind.EQUALS)
    if expect_res:is_err() then
      return ParseResult.err(expect_res:get_err())
    end

    local parse_res = self:parse_expr(scope)
    if parse_res:is_err() then
      return parse_res
    end

    id_val = parse_res:get_ok()
    if id_type then
      local err_loc_opt = self:check_node_type(id_type, id_val)
      if err_loc_opt:is_some() then
        return ParseResult.err(err_loc_opt:get_some())
      end
    else
      id_type = id_val.type
    end
  end

  local prev_token = self:peek_prev()

  if not id_type and not id_val then
    return ParseResult.err(new_error(
      ErrLocKind.SYNTAX,
      self,
      "Expected either a 'type' or '=' after ':'",
      prev_token,
      prev_token
    ))
  end

  local id_decl = new(Node)
  $id_decl = {
    kind = NodeKind.ID_DECL,

    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col
    },
    e_pos = {
      line = prev_token.e_pos.line,
      col = prev_token.e_pos.col
    },
    s_index = s_token.s_index,
    e_index = prev_token.e_index,
    scope = scope,

    type = id_type,
    name = id_name,
    val = id_val
  }
  return ParseResult.ok(id_decl)
end

function Parser:parse_assignment(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local parse_res = self:parse_id(scope)
  if parse_res:is_err() then
    return parse_res
  end
  local assignment_id = parse_res:get_ok()

  local expect_res = self:expect(TokenKind.EQUALS)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  parse_res = self:parse_expr(scope)
  if parse_res:is_err() then
    return parse_res
  end
  local assignment_val = parse_res:get_ok()

  local err_loc_opt = self:check_node_type(assignment_id.type, assignment_val)
  if err_loc_opt:is_some() then
    return ParseResult.err(err_loc_opt:get_some())
  end

  local assignment_node: *Node = new(Node)
  $assignment_node = {
    kind = NodeKind.ASSIGNMENT,

    s_pos = {
      line = assignment_id.s_pos.line,
      col = assignment_id.s_pos.col,
    },
    e_pos = {
      line = assignment_val.e_pos.line,
      col = assignment_val.e_pos.col,
    },
    scope = scope,

    s_index = assignment_id.s_index,
    e_index = assignment_val.e_index,

    id = assignment_id,
    val = assignment_val
  }

  return ParseResult.ok(assignment_node)
end

local function get_returns(scope: *Node, rets: *sequence(*Node))
  assert(scope and scope:is_scope())
  for _, child in ipairs(scope.children) do
    if child.kind == NodeKind.RETURN then
      rets:push(child)
    end

    if child:is_scope() then
      get_returns(child, rets)
    end
  end
end

function Parser:parse_proc_def(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.KEYWORD_PROC)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()
  local token = s_token

  local expect_res = self:expect(TokenKind.IDENTIFIER)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local proc_def_name = expect_res:get_ok().val

  local id_opt = Parser.find_id_decl_up_scope(scope, proc_def_name, token.s_index)
  if id_opt:is_some() then
    return ParseResult.err(error_redeclaration(self, token))
  end

  local proc_def = new(Node)

  $proc_def = {
    kind = NodeKind.PROC_DEF,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    s_index = s_token.s_index,
    scope = scope,

    name = proc_def_name,
  }

  expect_res = self:expect(TokenKind.OPEN_PAREN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  while not self:is({ TokenKind.CLOSE_PAREN }) do
    local parse_res: ParseResult = self:parse_id_decl(scope)
    if parse_res:is_err() then
      return parse_res
    end

    local param = parse_res:get_ok()
    proc_def.params:push(param)
    -- NOTE: Hack to make params available in scope
    proc_def.children:push(param)

    expect_res = self:advance_comma_unless(TokenKind.CLOSE_PAREN)
    if expect_res:is_err() then
      return ParseResult.err(expect_res:get_err())
    end
  end

  expect_res = self:expect(TokenKind.CLOSE_PAREN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  if self:is({ TokenKind.COLON }) then
    expect_res = self:expect(TokenKind.COLON)
    if expect_res:is_err() then
      return ParseResult.err(expect_res:get_err())
    end

    local parse_res = self:parse_type(scope)
    if parse_res:is_err() then
      return parse_res
    end
    proc_def.ret_t = parse_res:get_ok()
  end

  local proc_sig_end = self:peek_prev()

  if not proc_def.ret_t then
    assert(builtin_types:has("niltype"))
    proc_def.ret_t = builtin_types["niltype"]
  end

  local proc_type = new(Node)
  $proc_type = {
    kind = NodeKind.TYPE,
    name = "proc_def",
    is_proc = true,
    is_type = true,
    params = proc_def.params,
    ret_t = proc_def.ret_t,
    type = proc_type
  }
  proc_def.type = proc_type

  -- NOTE: Hack to make proc available in scope
  local proc_id_decl = new(Node)
  $proc_id_decl = {
    kind = NodeKind.ID_DECL,

    s_pos = {
      line = proc_def.s_pos.line,
      col = proc_def.s_pos.col,
    },
    e_pos = {
      line = proc_sig_end.e_pos.line,
      col = proc_sig_end.e_pos.col
    },
    s_index = proc_def.s_index,
    e_index = proc_sig_end.e_index,
    scope = scope,

    name = proc_def.name,
    val = proc_def,
    type = proc_def.type,
  }
  proc_def.children:push(proc_id_decl)

  expect_res = self:expect(TokenKind.OPEN_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  while not self:is({ TokenKind.CLOSE_CURLY }) do
    local parse_res = self:parse_scope(proc_def, TokenKind.CLOSE_CURLY)
    if parse_res:is_err() then
      return parse_res
    else
      break
    end
  end

  local rets: sequence(*Node)
  get_returns(proc_def, &rets)
  for _, ret in ipairs(rets) do
    local ret_stmt_val = ret.val
    if self:check_node_type(proc_def.ret_t, ret_stmt_val):is_some() then
      return ParseResult.err({
        kind = ErrLocKind.SEMANTIC,

        path = self.path,
        content = self.content,
        err = ("Return statement type '%s' does not match defined return type '%s'"):format(ret_stmt_val.type.name, proc_def.ret_t.name),

        s_pos = {
          line = ret_stmt_val.s_pos.line,
          col = ret_stmt_val.s_pos.col,
        },
        e_pos = {
          line = ret_stmt_val.e_pos.line,
          col = ret_stmt_val.e_pos.col,
        },
        s_index = ret_stmt_val.s_index,
        e_index = ret_stmt_val.e_index,
      })
    end
  end

  expect_res = self:expect(TokenKind.CLOSE_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  token = expect_res:get_ok()

  for _, param in ipairs(proc_def.params) do
    for i, child in ipairs(proc_def.children) do
      if param == child then
        proc_def.children:remove(i)
        break
      end
    end
  end
  proc_def.children:remove(1)

  proc_def.e_index = token.e_index
  proc_def.e_pos = {
    line = token.e_pos.line,
    col = token.e_pos.col,
  }
  proc_def.content = self.content:sub(proc_def.s_index, proc_def.e_index)

  return ParseResult.ok(proc_def)
end

function Parser:parse_return_node(scope: *Node): ParseResult

  local expect_res = self:expect(TokenKind.KEYWORD_RETURN)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()
  local token = s_token

  local node: *Node = new(Node)
  local _node: Node = $node
  _node = {
    s_pos = {
      line = token.s_pos.line,
      col = token.s_pos.col,
    },
    s_index = token.s_index,
    kind = NodeKind.RETURN,
    scope = scope,
  }

  local ret_value: *Node
  if self:peek().kind ~= TokenKind.CLOSE_CURLY then
    local parse_res = self:parse_expr(scope)
    if parse_res:is_err() then
      return parse_res
    end
    ret_value = parse_res:get_ok()
  end
  local token = self:peek_prev()

  _node.e_pos = {
    line = token.e_pos.line,
    col = token.e_pos.col,
  }
  _node.val = ret_value
  _node.e_index = token.e_index
  $node = _node
  return ParseResult.ok(node)
end

function Parser:parse_while_loop(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.KEYWORD_WHILE)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()

  local parse_res = self:parse_expr(scope)
  if parse_res:is_err() then
    return parse_res
  end
  local while_cond = parse_res:get_ok()

  expect_res = self:expect(TokenKind.OPEN_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local while_loop = new(Node)

  $while_loop = {
    kind = NodeKind.WHILE_LOOP,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    s_index = s_token.s_index,
    scope = scope,

    cond = while_cond
  }

  while not self:is({ TokenKind.CLOSE_CURLY }) do
    parse_res = self:parse_scope(while_loop, TokenKind.CLOSE_CURLY)
    if parse_res:is_err() then
      return parse_res
    else
      break
    end
  end

  expect_res = self:expect(TokenKind.CLOSE_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  while_loop.e_index = e_token.e_index
  while_loop.e_pos = {
    line = e_token.e_pos.line,
    col = e_token.e_pos.col,
  }
  while_loop.content = self.content:sub(while_loop.s_index, while_loop.e_index)

  return ParseResult.ok(while_loop)
end

function Parser:parse_for_loop(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.KEYWORD_FOR)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()

  local parse_res = self:parse_id_decl(scope)
  if parse_res:is_err() then
    return parse_res
  end

  local for_loop_id = parse_res:get_ok()
  if not for_loop_id.type.is_integral then
    error()
  end

  local expect_res = self:expect(TokenKind.COMMA)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  parse_res = self:parse_expr(scope)
  if parse_res:is_err() then
    return parse_res
  end

  local for_loop_cond = parse_res:get_ok()
  if not for_loop_cond.type.is_integral then
    error()
  end

  local for_loop_step: *Node
  if self:is({ TokenKind.COMMA }) then
    self:advance()
    parse_res = self:parse_expr(scope)
    if parse_res:is_err() then
      return parse_res
    end
    for_loop_step = parse_res:get_ok()
    if not for_loop_step.type.is_integral then
      error()
    end
  end

  expect_res = self:expect(TokenKind.OPEN_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local for_loop = new(Node)

  $for_loop = {
    kind = NodeKind.FOR_LOOP,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    s_index = s_token.s_index,
    scope = scope,

    id = for_loop_id,
    cond = for_loop_cond,
    step = for_loop_step
  }

  for_loop.children:push(for_loop_id)

  while not self:is({ TokenKind.CLOSE_CURLY }) do
    parse_res = self:parse_scope(for_loop, TokenKind.CLOSE_CURLY)
    if parse_res:is_err() then
      return parse_res
    else
      break
    end
  end

  -- First children is always the loop id
  for_loop.children:remove(1)

  expect_res = self:expect(TokenKind.CLOSE_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  for_loop.e_index = e_token.e_index
  for_loop.e_pos = {
    line = e_token.e_pos.line,
    col = e_token.e_pos.col,
  }
  for_loop.content = self.content:sub(for_loop.s_index, for_loop.e_index)

  return ParseResult.ok(for_loop)
end

function Parser:parse_if_stmt(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.KEYWORD_IF)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()

  local parse_res = self:parse_expr(scope)
  if parse_res:is_err() then
    return parse_res
  end
  local if_cond = parse_res:get_ok()

  expect_res = self:expect(TokenKind.OPEN_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local if_stmt = new(Node)

  $if_stmt = {
    kind = NodeKind.IF_STMT,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    s_index = s_token.s_index,
    scope = scope,

    cond = if_cond
  }

  while not self:is({ TokenKind.CLOSE_CURLY }) do
    parse_res = self:parse_scope(if_stmt, TokenKind.CLOSE_CURLY)
    if parse_res:is_err() then
      return parse_res
    else
      break
    end
  end

  expect_res = self:expect(TokenKind.CLOSE_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  if_stmt.e_index = e_token.e_index
  if_stmt.e_pos = {
    line = e_token.e_pos.line,
    col = e_token.e_pos.col,
  }
  if_stmt.content = self.content:sub(if_stmt.s_index, if_stmt.e_index)

  return ParseResult.ok(if_stmt)
end

function Parser:parse_elif_stmt(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.KEYWORD_ELIF)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()

  if scope.children[#scope.children].kind ~= NodeKind.IF_STMT then
    return ParseResult.err(new_error(ErrLocKind.SYNTAX, self, "if statement expected before elif", s_token, s_token))
  end

  local parse_res = self:parse_expr(scope)
  if parse_res:is_err() then
    return parse_res
  end
  local elif_cond = parse_res:get_ok()

  expect_res = self:expect(TokenKind.OPEN_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local elif_stmt = new(Node)

  $elif_stmt = {
    kind = NodeKind.ELIF_STMT,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    s_index = s_token.s_index,
    scope = scope,

    cond = elif_cond
  }

  while not self:is({ TokenKind.CLOSE_CURLY }) do
    parse_res = self:parse_scope(elif_stmt, TokenKind.CLOSE_CURLY)
    if parse_res:is_err() then
      return parse_res
    else
      break
    end
  end

  expect_res = self:expect(TokenKind.CLOSE_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  elif_stmt.e_index = e_token.e_index
  elif_stmt.e_pos = {
    line = e_token.e_pos.line,
    col = e_token.e_pos.col,
  }
  elif_stmt.content = self.content:sub(elif_stmt.s_index, elif_stmt.e_index)

  return ParseResult.ok(elif_stmt)
end

function Parser:parse_else_stmt(scope: *Node): ParseResult
  assert(scope and scope:is_scope())

  local expect_res = self:expect(TokenKind.KEYWORD_ELSE)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local s_token = expect_res:get_ok()

  if scope.children[#scope.children].kind ~= NodeKind.IF_STMT and scope.children[#scope.children].kind ~= NodeKind.ELIF_STMT then
    return ParseResult.err(new_error(ErrLocKind.SYNTAX, self, "either if or elif statement expected before else", s_token, s_token))
  end

  expect_res = self:expect(TokenKind.OPEN_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end

  local else_stmt = new(Node)

  $else_stmt = {
    kind = NodeKind.ELSE_STMT,
    s_pos = {
      line = s_token.s_pos.line,
      col = s_token.s_pos.col,
    },
    s_index = s_token.s_index,
    scope = scope,
  }

  while not self:is({ TokenKind.CLOSE_CURLY }) do
    local parse_res = self:parse_scope(else_stmt, TokenKind.CLOSE_CURLY)
    if parse_res:is_err() then
      return parse_res
    else
      break
    end
  end

  expect_res = self:expect(TokenKind.CLOSE_CURLY)
  if expect_res:is_err() then
    return ParseResult.err(expect_res:get_err())
  end
  local e_token = expect_res:get_ok()

  else_stmt.e_index = e_token.e_index
  else_stmt.e_pos = {
    line = e_token.e_pos.line,
    col = e_token.e_pos.col,
  }
  else_stmt.content = self.content:sub(else_stmt.s_index, else_stmt.e_index)

  return ParseResult.ok(else_stmt)
end

Parser.parse_scope = function(self: *Parser, scope: *Node, till: TokenKind): ParseResult
  assert(scope and scope:is_scope())
  range_check!(till, TokenKind)

  local token = self:peek()
  if token.kind == till then
    return ParseResult.ok(scope)
  end

  local parse_res: ParseResult = _switch!(token.kind, {
    [TokenKind.COMMENT] = (do
      self:advance()
      local node: *Node = new(Node)
      $node = {
        s_pos = {
          line = token.s_pos.line,
          col = token.s_pos.col,
        },
        e_pos = {
          line = token.e_pos.line,
          col = token.e_pos.col,
        },
        s_index = token.s_index,
        e_index = token.e_index,
        kind = NodeKind.COMMENT,
        scope = scope,
      }
      in ParseResult.ok(node)
    end),
    [TokenKind.IDENTIFIER] = (do
      if self:peek_next().kind == TokenKind.COLON then
        in self:parse_id_decl(scope)
      elseif self:peek_next().kind == TokenKind.OPEN_PAREN then
        in self:parse_proc_call(scope)
      elseif self:peek_next().kind == TokenKind.EQUALS then
        in self:parse_assignment(scope)
      end
      in self:parse_id(scope)
    end),
    [TokenKind.KEYWORD_PROC] = self:parse_proc_def(scope),
    [TokenKind.KEYWORD_RETURN] = self:parse_return_node(scope),
    [TokenKind.KEYWORD_IF] = self:parse_if_stmt(scope),
    [TokenKind.KEYWORD_ELIF] = self:parse_elif_stmt(scope),
    [TokenKind.KEYWORD_ELSE] = self:parse_else_stmt(scope),
    [TokenKind.KEYWORD_WHILE] = self:parse_while_loop(scope),
    [TokenKind.KEYWORD_BREAK] = (do
      self:advance()
      if not scope:is_loop() then
        return ParseResult.err(new_error(ErrLocKind.SEMANTIC, self, "break statements must be within a loop", token, token))
      end
      local node: *Node = new(Node)
      $node = {
        kind = NodeKind.BREAK,

        s_pos = {
          line = token.s_pos.line,
          col = token.s_pos.col,
        },
        e_pos = {
          line = token.e_pos.line,
          col = token.e_pos.col,
        },
        s_index = token.s_index,
        e_index = token.e_index,

        scope = scope,
      }
      in ParseResult.ok(node)
    end),
    [TokenKind.KEYWORD_FOR] = self:parse_for_loop(scope),
    _default = (do
      error(("Unexpected Tokenkind '%s'"):format(TokenKind_tostring(token.kind)))
      in ParseResult.err({})
    end)
  })

  if parse_res:is_err() then
    return parse_res
  end

  local child = parse_res:get_ok()
  scope.children:push(child)

  return self:parse_scope(scope, till)
end

function Parser:parse(): ParseResult
  local ast = new(Node)
  local s_line, s_col = pos_to_line_col(1, self.content)
  local e_line, e_col = pos_to_line_col(#self.content, self.content)
  $ast = {
    kind = NodeKind.AST,

    s_pos = {
      line = s_line,
      col = s_col
    },
    e_pos = {
      line = e_line,
      col = e_col
    },
    s_index = 1,
    e_index = #self.content,
    scope = nilptr,

    content = self.content,
  }
  local res = self:parse_scope(ast, TokenKind.EOF)
  if res:is_err() then
    return ParseResult.err(res:get_err())
  end
  return ParseResult.ok(res:get_ok())
end

local Parser.NodeKind = NodeKind
local Parser.NodeKind_tostring = NodeKind_tostring
local Parser.Node = Node
local Parser.builtin_types = builtin_types
return Parser
